/* 
BEGINCOPYRIGHT X
	
	Copyright (c) 2004-2005, Xilinx Inc.
	All rights reserved.
	
	Redistribution and use in source and binary forms, 
	with or without modification, are permitted provided 
	that the following conditions are met:
	- Redistributions of source code must retain the above 
	  copyright notice, this list of conditions and the 
	  following disclaimer.
	- Redistributions in binary form must reproduce the 
	  above copyright notice, this list of conditions and 
	  the following disclaimer in the documentation and/or 
	  other materials provided with the distribution.
	- Neither the name of the copyright holder nor the names 
	  of its contributors may be used to endorse or promote 
	  products derived from this software without specific 
	  prior written permission.
	
	THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND 
	CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, 
	INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF 
	MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE 
	DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR 
	CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, 
	SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT 
	NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; 
	LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) 
	HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN 
	CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR 
	OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS 
	SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
	
ENDCOPYRIGHT
*/
// Transpose.cal
//
// Author: David B. Parlour (dave.parlour@xilinx.com)
//

package com.xilinx.iDCT;

actor Transpose () int(size=sz) X0, int(size=sz) X1, int(size=sz) X2, int(size=sz) X3 ==>
int(size=sz) Y0 , int(size=sz) Y1 :

	int sz = 16;

	int(size=sz) mem[2][8][8];

	int rcount := 0;

	int ccount := 0;

	uint(size=1) select := 0;

	action X0:[ a ], X1:[ b ], X2:[ c ], X3:[ d ] ==>
	guard
		rcount < 64
	do
		int row = (rcount >> 3);
		int quad = (rcount >> 2) & 1;

		// { 0, 7, 3, 4 }
		// { 1, 6, 2, 5 }
		if quad = 0 then
			mem[select][row][0] := a;
			mem[select][row][7] := b;
			mem[select][row][3] := c;
			mem[select][row][4] := d;
		else
			mem[select][row][1] := a;
			mem[select][row][6] := b;
			mem[select][row][2] := c;
			mem[select][row][5] := d;
		end
		rcount := rcount + 4;
	end

	action ==> Y0:[ mem[i][a][col] ], Y1:[ mem[i][b][col] ]
	guard
		ccount > 0
	do
		int col = (64 - ccount) >> 3;
		int pair = ((64 - ccount) >> 1) & 3;
		int i = (select ^ 1);

		// idct col input order is
		// { 0, 4 } {2, 6} {1, 7} {5, 3}
		uint(size=3) a =
			if pair = 0 then 0
				else if pair = 1 then 2
				else if pair = 2 then 1
				else 5 end
				end
			end;

		uint(size=3) b =
			if pair = 0 then 4
				else if pair = 1 then 6
				else if pair = 2 then 7
				else 3 end
				end
			end;

		ccount := ccount - 2;
	end

	action ==>
	guard
		ccount = 0 and rcount = 64
	do
		select := (select ^ 1);
		ccount := 64;
		rcount := 0;
	end

end